apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.techflow.materialcolor"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 220030202
        versionName "3.2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            if (project.hasProperty('MYAPP_UPLOAD_STORE_FILE')) {
                storeFile file(MYAPP_UPLOAD_STORE_FILE)
                storePassword MYAPP_UPLOAD_STORE_PASSWORD
                keyAlias MYAPP_UPLOAD_KEY_ALIAS
                keyPassword MYAPP_UPLOAD_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            //resConfigs "en"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            FirebasePerformance {
                // Set this flag to 'false' to disable @AddTrace annotation processing and
                // automatic HTTP/S network request monitoring
                // for a specific build variant at compile time.
                instrumentationEnabled false
            }
        }
    }
    buildTypes.each {
        it.buildConfigField('String', 'AUDIENCE_BANNER_ID', AUDIENCE_BANNER_ID)
        it.buildConfigField('String', 'AUDIENCE_INTERSTITIAL_ID', AUDIENCE_INTERSTITIAL_ID)
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.android.billingclient:billing:2.1.0'                    // In app purchase
    implementation 'com.android.billingclient:billing-ktx:2.1.0'
    implementation 'androidx.browser:browser:1.0.0'                             // Chrome custom tab
    implementation 'com.google.android.play:core:1.7.3'                         // Play store core
    implementation 'com.google.android.play:core-ktx:1.7.0'

    // Firebase
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:25.12.0')

    implementation 'com.google.firebase:firebase-analytics-ktx'          // Analytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'        // Crashlytics
    implementation 'com.google.firebase:firebase-perf-ktx'               // Performance
    implementation 'com.google.firebase:firebase-config-ktx'             // Remote Config

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Third party
    implementation 'com.irozon.justbar:justbar:1.0.1'                       // Bottom bar
    implementation 'com.balysv:material-ripple:1.0.2'                       // Ripple effect
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'     // Image cropper
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'     // Tap target view
    implementation 'com.afollestad.material-dialogs:core:3.1.1'             // Material dialog
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.1'     // Material bottom sheet
    implementation 'com.afollestad.material-dialogs:color:3.1.1'
    implementation 'com.mikhaellopez:circularimageview:3.2.0'               // Circle Image View
    implementation 'com.android.support:support-annotations:28.0.0'         // Required Dependency by Audience Network SDK
    implementation 'com.facebook.android:audience-network-sdk:5.+'          // Facebook audience
    implementation 'com.github.sephiroth74:android-target-tooltip:2.0.4'    // Tooltip
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'               // Page indicator (view pager)

    def room_version = "1.1.1"                                              // Room database
    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    def arch_life_cycle_version = "1.1.1"                                   // LiveData, ViewModel
    implementation "android.arch.lifecycle:extensions:$arch_life_cycle_version"
    kapt "android.arch.lifecycle:compiler:$arch_life_cycle_version"
}
apply plugin: 'com.google.gms.google-services'
